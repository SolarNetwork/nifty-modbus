import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'eclipse'
    id 'biz.aQute.bnd.builder'
}

apply plugin: 'java-library'
apply plugin: 'signing'
apply plugin: 'com.vanniktech.maven.publish'    

description = 'Nifty Modbus: RTU'

dependencies {
	implementation project(':nifty-modbus-api')
	implementation project(':nifty-modbus-core');
	implementation "org.slf4j:slf4j-api:${slf4jVersion}"
	implementation "io.netty:netty-codec:${nettyVersion}"
	implementation "io.netty:netty-handler:${nettyVersion}"

	// Testing
	testImplementation project(':nifty-modbus-test')
}

jar.manifest {
    attributes('Implementation-Version': version)
}

tasks.withType(Jar).matching { it.name.contains('sources') }.configureEach {
	manifest {
		attributes(
			'Bundle-SymbolicName': 'net.solarnetwork.common.io.modbus.rtu.sources',
			'Bundle-Name': 'SolarNetwork Modbus RTU (Core) Sources',
			'Bundle-Version': version,
			'Bundle-ManifestVersion': '2',
			'Eclipse-SourceBundle': 'net.solarnetwork.common.io.modbus.rtu;version="'
				+ version + '";roots:="."'
		)
	}
}

eclipse {
	jdt {
		javaRuntimeName 'JavaSE-1.8'
	}
}

mavenPublishing {
	coordinates(group, project.name, version)
	publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
	signAllPublications()

	pom {
		name = project.description
		description = 'Nifty Modbus RTU (serial network) support.'
		url = 'https://github.com/SolarNetwork/nifty-modbus/rtu'
		inceptionYear = '2024'
		licenses {
			license {
				name = 'GNU General Public License, Version 2.0'
				url = 'https://opensource.org/licenses/GPL-2.0'
			}
		}
		developers {
			developer {
				id = 'msqr'
				name = 'Matt Magoffin'
				email = 'matt@solarnetwork.net'
				organization = 'SolarNetwork Foundation'
				organizationUrl = 'https://solarnetwork.org.nz'
			}
		}
		scm {
			connection = 'scm:git:https://github.com/SolarNetwork/nifty-modbus.git'
			developerConnection = 'scm:git:git@github.com:SolarNetwork/nifty-modbus.git'
			url = 'https://github.com/SolarNetwork/nifty-modbus'
		}
	}
}

signing {
	useGpgCmd()
}
